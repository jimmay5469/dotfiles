"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" vim-plug
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
call plug#begin('~/.vim/plugged')

"Spacemacs (use local vim-spacemacs if possible)
if !empty(glob("~/repos/vim-spacemacs"))
  Plug '~/repos/vim-spacemacs'
else
  Plug 'jimmay5469/vim-spacemacs'
endif

"Key binding menues
Plug 'hecal3/vim-leader-guide'

"Good defaults
Plug 'tpope/vim-sensible'
"Plug 'PreserveNoEOL' "required for editorconfig-vim
Plug 'editorconfig/editorconfig-vim'

"Navigation
Plug 'tpope/vim-vinegar'
Plug 'kien/ctrlp.vim'
Plug 'rking/ag.vim'

"Pane navigation
Plug 'christoomey/vim-tmux-navigator'

"Theme
"Plug 'romainl/Apprentice'
Plug 'chriskempson/base16-vim'
Plug 'ap/vim-css-color'

"Git
Plug 'tpope/vim-fugitive'

"Syntax hightlighting
Plug 'sheerun/vim-polyglot'

"Better autochdir
Plug 'airblade/vim-rooter'

"Autocomplete
if has('nvim')
  Plug 'Shougo/deoplete.nvim', { 'do': ':UpdateRemotePlugins' }
endif
Plug 'jiangmiao/auto-pairs'
Plug 'alvan/vim-closetag'
Plug 'tpope/vim-surround'

call plug#end()


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Settings
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

"fix copy paste error in osx sierra
set clipboard=unnamed

"line numbers
set number

"tabs
set tabstop=2
set shiftwidth=2

"highlight search
set hlsearch

":e autocomplete settings
set wildmenu
set wildmode=longest:list,full

"don't create swap files
set nobackup
set nowritebackup
set noswapfile

"Ignores
set wildignore+=*tmp/*,*node_modules/*,*bower_components/*,*.git/*,*.pyc,*lib/python*

"EditorConfig settings
let g:EditorConfig_exclude_patterns = ['fugitive://.*']

"timeout a little faster because we have vim-leader-guide
set timeoutlen=300

"better autochdir
let g:rooter_silent_chdir = 1

"folding
set foldmethod=indent
set nofoldenable

"autocomplete
if has('nvim')
  " Use deoplete.
  let g:deoplete#enable_at_startup = 1
  let g:deoplete#enable_ignore_case = 1
  inoremap <expr><tab> pumvisible() ? "\<c-n>" : "\<tab>"
endif
let g:closetag_filenames = "*.html,*.js,*.jsx"


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Statusline/Tabline
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
function! ShortPath(path)
  let cwd = substitute(a:path, $HOME, "~", "")
  return substitute(cwd, "\\~/repos/", "", "")
endfunction
function! ShortFilepath()
  return ShortPath(expand("%f"))
endfunction
function! ShortCWD()
  return ShortPath(getcwd())
endfunction
function! IsModified()
  return &modified?"[+]":""
endfunction

"Statusline
function! StatusLine()
  let s = ''
  let s .= '%{IsModified()}%r' "File status
  let s .= ' %{ShortFilepath()}'
  let s .= '%=%L ' "Right align line numbers
  return s
endfunction
set statusline=%!StatusLine()

"Tabline
set showtabline=2
function! Tabline()
  let s = ''
  for i in range(tabpagenr('$'))
    let tab = i + 1
    let winnr = tabpagewinnr(tab)
    let buflist = tabpagebuflist(tab)
    let bufnr = buflist[winnr - 1]
    let bufname = bufname(bufnr)

    let s .= '%' . tab . 'T'
    let s .= (tab == tabpagenr() ? '%#TabLineSel#' : '%#TabLine#')
    let s .= (bufname != '' ? '['. fnamemodify(bufname, ':t') . ']' : '[No Name]')
  endfor

  let s .= '%#TabLineFill#'

  " right-align the CWD
  let s .= '%=%#TabLineSel#[%{ShortCWD()}]'

  return s
endfunction
set tabline=%!Tabline()


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Style
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

"Syntax highlighting
if filereadable(expand("~/.vimrc_background"))
  let base16colorspace=256
  source ~/.vimrc_background
endif

"gvim options
if has("gui_running")
  "remove scrollbars
  set guioptions-=l
  set guioptions-=L
  set guioptions-=r
  set guioptions-=b

  "set fonts and background
  set transparency=5
  set lines=70
  set columns=250
endif


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" Key Bindings
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
let mapleader = "\<SPACE>"

let g:spacemacs#plugins = [
  \ 'kien/ctrlp.vim',
  \ 'rking/ag.vim',
  \ 'tpope/vim-fugitive',
\ ]
let g:spacemacs#excludes = [
  \ '^br',
  \ '^fed',
  \ '^fs',
  \ '^pp',
\ ]

nnoremap <LEADER>br :checktime<CR>
nnoremap <LEADER>fs :wa<CR>
nnoremap <LEADER>fed :e ~/repos/dotfiles/.vimrc.symlink<CR>
nnoremap <LEADER>pp :e ~/repos/

"Key binding menues
nnoremap <silent> <LEADER> :<C-U>LeaderGuide '<SPACE>'<CR>
vnoremap <silent> <LEADER> :<C-U>LeaderGuideVisual '<SPACE>'<CR>

"Reload vim-spacemacs if it is local
if !empty(glob("~/repos/vim-spacemacs"))
  let g:spacemacs#excludes = add(g:spacemacs#excludes, '^feR')
  nnoremap <LEADER>feR :source ~/.vimrc<CR>:source ~/repos/vim-spacemacs/plugin/spacemacs.vim<CR>
endif
